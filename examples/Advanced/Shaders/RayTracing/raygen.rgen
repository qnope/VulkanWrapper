#version 460 core
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : enable

layout(set = 0, binding = 0) uniform accelerationStructureEXT as;
layout(set = 0, binding = 1, rgba32f) uniform image2D lightBuffer;
layout(set = 0, binding = 2, std140) uniform Block {
    mat4 invView;
    mat4 invPerspective;
};

layout(location = 0) rayPayloadEXT vec4 payload;

void main() {
    ivec2 pixel = ivec2(gl_LaunchIDEXT.xy);
    vec2 uv = 2.0 * pixel / vec2(gl_LaunchSizeEXT.xy) - 1.0;

    vec3 origin = vec3(invView * vec4(0, 0, 0, 1.0));
    vec4 viewFar = invPerspective * vec4(uv, 1.0, 1.0);
    vec4 viewNear = invPerspective * vec4(uv, 0.0, 1.0);

    viewFar /= viewFar.w;
    viewNear /= viewNear.w;

    vec3 direction = mat3(invView) * normalize(viewFar.xyz - viewNear.xyz);

    uint flags = gl_RayFlagsOpaqueEXT;

    traceRayEXT(as, flags, 0xff, 
                0, 0, 0, 
                origin, 
                0.1,
                direction,
                1000.0, 
                0);        
    imageStore(lightBuffer, pixel, payload);
} 